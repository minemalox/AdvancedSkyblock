import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'http://repo.spongepowered.org/maven' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath "io.franzbecker:gradle-lombok:4.0.0"
    }
}

plugins {
    id 'java'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "io.franzbecker.gradle-lombok"
apply plugin: 'idea'
idea {
    module {
        inheritOutputDirs = true
    }
}


group = 'dev.minemalox.advancedskyblock'

// Java plugin settings
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

minecraft {

    //mappings channel: 'snapshot', version: project.mappings
    mappings channel: 'snapshot', version: '20180814-1.12'
    accessTransformer = file('src/main/resources/META-INF/advancedskyblock_at.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'fml.coreMods.load', 'dev.minemalox.advancedskyblock.AdvancedSkyblockLoadingPlugin'

        }
    }

}

repositories {
    maven {
        name 'JitPack'
        url 'https://jitpack.io'
    }
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'

    implementation('com.github.BiscuitDevelopment:DiscordIPC:139c9d6') {
        exclude module: 'gson'
    }

    compile 'org.spongepowered:mixin:0.8.1-SNAPSHOT'
}

sourceSets {
    main {
        ext.refMap = "mixins.advancedskyblock.refmap.json"
    }
}

jar {
    baseName = project.modName + "-" + project.minecraftVersion + "-" + version + ".jar"
    // enabled = false
}


shadowJar {
    archiveName = tasks.jar.archiveName
    classifier = ''

    manifest.attributes(
            'Main-Class': 'OpenErrorMessage',
            'ModSide': 'CLIENT',
            'MixinConfigs': 'mixins.advancedskyblock.json',
            'FMLCorePluginContainsFMLMod': true,
            'FMLCorePlugin': 'dev.minemalox.advancedskyblock.AdvancedSkyblockLoadingPlugin',
            'ForceLoadAsMod': true,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'FMLAT': "${project.modId}_at.cfg"
    )

    exclude 'dummyThing'
    exclude 'META-INF/maven/'
    exclude 'META-INF/nar/'
    exclude 'module-info.class'
    exclude 'META-INF/versions/'

    minimize()
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = project.group + '.' + modId + '.shadow'
}

shadowJar.dependsOn tasks.relocateShadowJar
tasks.build.dependsOn(shadowJar)

// tasks.reobfJar.dependsOn(tasks.shadowJar)
//reobf { // Only for labymod client.
//    jar {
//        useNotchSrg()
//    }
//}